option(USE_MIDI_FLUIDSYNTH "Build with FluidSynth wave table MIDI sequencer support" OFF)
if(USE_MIDI_FLUIDSYNTH)
    if(USE_SYSTEM_AUDIO_LIBRARIES)
        find_path(LIBFLUIDSYNTH_INCLUDE_DIR "fluidsynth.h")
        find_library(LIBFLUIDSYNTH_LIB NAMES fluidsynth)
        if(LIBFLUIDSYNTH_INCLUDE_DIR AND LIBFLUIDSYNTH_LIB)
            set(FOUND_FLUIDSYNTH 1)
        endif()
        message("FluidSynth: [${FOUND_FLUIDSYNTH}] ${LIBFLUIDSYNTH_INCLUDE_DIR} ${LIBFLUIDSYNTH_LIB}")
    else()
        set(FOUND_FLUIDSYNTH 1)
        if(DOWNLOAD_AUDIO_CODECS_DEPENDENCY)
            set(LIBFLUIDSYNTH_LIB fluidsynth)
        else()
            find_library(LIBFLUIDSYNTH_LIB NAMES fluidsynth
                         HINTS "${AUDIO_CODECS_INSTALL_PATH}/lib")
        endif()
    endif()
    if(FOUND_FLUIDSYNTH)
        message("== using FluidSynth ==")
        add_definitions(-DMUSIC_MID_FLUIDSYNTH)
        if(AUDIO_CODECS_REPO_PATH)
            list(APPEND SDL_MIXER_INCLUDE_PATHS ${AUDIO_CODECS_PATH}/FluidLite/include)
        endif()
        list(APPEND SDLMixerX_LINK_LIBS ${LIBFLUIDSYNTH_LIB})
        list(APPEND SDLMixerX_SOURCES
            ${CMAKE_CURRENT_LIST_DIR}/music_fluidsynth.c)
    endif()
endif()
