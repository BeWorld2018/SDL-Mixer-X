option(USE_MODPLUG         "Build with ModPlug library" ON)
if(USE_MODPLUG)
    option(USE_MODPLUG_DYNAMIC "Use dynamical loading of ModPlug" OFF)
    option(USE_MODPLUG_STATIC "Use linking with a static ModPlug" ON)

    if(USE_SYSTEM_AUDIO_LIBRARIES)
        find_package(ModPlug QUIET)
        message("ModPlug: [${ModPlug_FOUND}] ${ModPlug_INCLUDE_DIRS} ${ModPlug_LIBRARIES}")
        if(USE_MODPLUG_DYNAMIC)
            add_definitions(-DMODPLUG_DYNAMIC=\"${ModPlug_DYNAMIC_LIBRARY}\")
            message("Dynamic ModPlug: ${ModPlug_DYNAMIC_LIBRARY}")
        endif()
    else()
        if(DOWNLOAD_AUDIO_CODECS_DEPENDENCY)
            set(ModPlug_LIBRARIES modplug)
        else()
            find_library(ModPlug_LIBRARIES NAMES modplug
                         HINTS "${AUDIO_CODECS_INSTALL_PATH}/lib")
        endif()
        set(ModPlug_FOUND 1)
        set(ModPlug_INCLUDE_DIRS "${AUDIO_CODECS_PATH}/libmodplug/include")
    endif()

    if(ModPlug_FOUND)
        message("== using libModPlug ==")
        add_definitions(-DMUSIC_MOD_MODPLUG)
        if(USE_MODPLUG_STATIC)
            add_definitions(-DMODPLUG_STATIC)
        endif()
        if(NOT USE_SYSTEM_AUDIO_LIBRARIES OR NOT USE_MODPLUG_DYNAMIC)
            list(APPEND SDLMixerX_LINK_LIBS ${ModPlug_LIBRARIES})
        endif()
        list(APPEND SDL_MIXER_INCLUDE_PATHS ${ModPlug_INCLUDE_DIRS})
        list(APPEND SDLMixerX_SOURCES
            ${SDLMixerX_SOURCE_DIR}/src/codecs/music_modplug.c)
    else()
        message("-- skipping libModPlug --")
    endif()
endif()
